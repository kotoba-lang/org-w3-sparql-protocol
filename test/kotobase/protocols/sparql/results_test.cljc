(ns kotobase.protocols.sparql.results-test
  (:require [clojure.test :refer [deftest is]]
            [kotobase.protocols.sparql.quads :as quads]
            [kotobase.protocols.sparql.results :as results]))

(deftest term->binding-shapes
  (is (= {"type" "uri" "value" "urn:kotobase:users/u1"}
         (results/term->binding (quads/iri "urn:kotobase:users/u1"))))
  (is (= {"type" "literal" "value" "Alice"}
         (results/term->binding (quads/->literal "Alice"))))
  (is (= {"type" "literal" "value" "900000" "datatype" "http://www.w3.org/2001/XMLSchema#integer"}
         (results/term->binding (quads/->literal 900000))))
  (is (= {"type" "literal" "value" "true" "datatype" "http://www.w3.org/2001/XMLSchema#boolean"}
         (results/term->binding (quads/->literal true)))))

(deftest select->json-shape
  (let [rows [{'?s (quads/iri "urn:kotobase:users/u1") '?n (quads/->literal "Alice")}]
        out (results/select->json '[?s ?n] rows)]
    (is (= {"vars" ["s" "n"]} (get out "head")))
    (is (= [{"s" {"type" "uri" "value" "urn:kotobase:users/u1"}
             "n" {"type" "literal" "value" "Alice"}}]
           (get-in out ["results" "bindings"])))))

(deftest select->json-star-derives-vars-from-rows
  (let [rows [{'?s (quads/iri "urn:kotobase:users/u1") '?n (quads/->literal "Alice")}]
        out (results/select->json nil rows)]
    (is (= ["s" "n"] (get-in out ["head" "vars"])))))

(deftest select->json-empty-results
  (let [out (results/select->json '[?s] [])]
    (is (= [] (get-in out ["results" "bindings"])))))

(deftest ask->json-shape
  (is (= {"head" {} "boolean" true} (results/ask->json true)))
  (is (= {"head" {} "boolean" false} (results/ask->json false))))
